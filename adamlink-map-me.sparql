#+ endpoint: https://api.druid.datalegend.net/datasets/AdamNet/all/services/endpoint/sparql 

PREFIX bif: <http://www.openlinksw.com/schemas/bif#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX sem: <http://semanticweb.cs.vu.nl/2009/11/sem/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

select ?kaart ?img ?x ?y ?title {
  ?kaart dct:spatial ?spatial .
  ?kaart foaf:depiction ?img .
  ?kaart dc:title ?title .
  ?spatial dc:type "outline"^^xsd:string .
  ?spatial geo:hasGeometry/geo:asWKT ?wktmap .
  ?spatial wdt:P2046 ?km2 .
  bind (bif:st_geomfromtext("POINT(4.895168 52.370216)") as ?x)
  bind (bif:st_geomfromtext(?wktmap) as ?y)
  FILTER (bif:st_intersects(?x, ?y))
}
ORDER BY ASC(?km2)
limit 5
